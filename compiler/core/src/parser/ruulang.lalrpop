use crate::parser::{
    ruulang_ast::{Grant, Rule, Attribute, Entrypoint, Fragment},
    parser_constructs::ParserStatement,
    parse_location::{Parsed, IdentifierKind, Identifier},
    schema_ast::{Entity, Relationship},
    parser_utils::parse_docstrings
};

grammar;

match {
    r"//[^\n\r]*[\n\r]*" => LineComment,
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => BlockComment,
} else {
    r"\s*" => { },
    _ // Everything else
}

pub Term: Vec<ParserStatement> = {
    <e: (<Entry>)*> => e
};

pub Entry: ParserStatement = {
    <f:CommentedFrag> => ParserStatement::Fragment(f),
    <e:CommentedEntrypoint> => ParserStatement::Entrypoint(e),
    <e:CommentedEntity> => ParserStatement::Entity(e),
}

pub Entrypoint: Parsed<Entrypoint> = {
    <l:@L>
        "@" <s:EntityIdentifier> "{"
            <rs: (<CommentedRule>)*>
            (<Comment>)*
        "}"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Entrypoint {
            entrypoint: s,
            rules: rs
        }
    )
}

pub CommentedEntrypoint: Parsed<Entrypoint> = {
    <c: (<Comment>)*> <e: Entrypoint> => e.into_with_docstring(parse_docstrings(c)).0
}

pub Frag: Parsed<Fragment> = {
    <l:@L>
        "fragment" <s:FragmentIdentifier> "for" <e:EntityIdentifier> "{"
            <g: (<CommentedGrant>)*>
            <c: (<CommentedRule>)*>
        "}"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Fragment {
            name: s,
            for_entity: e,
            grants: g,
            rules: c,
        },
    )
}

pub CommentedFrag: Parsed<Fragment> = {
    <c: (<Comment>)*> <f: Frag> => f.into_with_docstring(parse_docstrings(c)).0
}

pub Rel: Parsed<Rule> = {
    <l:@L>
        <s:RuleIdentifier> <a: (<CommentedAttr>)*> "{"
            <g: (<CommentedGrant>)*>
            <f: (<CommentedFragmentInclude>)*>
            <c: (<CommentedRule>)*>
        "}"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Rule {
            relationship: s,
            attributes: a,
            grants: g,
            rules: c,
            include_fragments: f,
            recursive: false,
        },
    )
}

pub Uni: Parsed<Rule> = {
    <l1:@L>
        <s:RuleIdentifier> <a: (<CommentedAttr>)*> "{"
            <l2:@L>
                "*"
            <r2:@R>
        "}"
    <r1:@R> => Parsed::new_at_loc(
        (l1, r1),
        Rule {
            relationship: s,
            attributes: a,
            grants: vec![
                Parsed {
                    loc: None,
                    docstring: None,
                    file_name: None,
                    data: Grant::new(vec!["read".to_string()])
                },
                Parsed {
                    loc: None,
                    docstring: None,
                    file_name: None,
                    data: Grant::new(vec!["write".to_string()])
                },
            ],
            rules: vec![Parsed::new_at_loc(
                (l2, r2),
                Rule {
                    relationship: Parsed::new_at_loc(
                        (l2, r2),
                        Identifier::new(
                            IdentifierKind::Rule,
                            "*".to_string(),
                        )
                    ),
                    attributes: vec![],
                    grants: vec![
                        Parsed {
                            loc: None,
                            docstring: None,
                            file_name: None,
                            data: Grant::new(vec!["read".to_string()])
                        },
                        Parsed {
                            loc: None,
                            docstring: None,
                            file_name: None,
                            data: Grant::new(vec!["write".to_string()])
                        },
                    ],
                    rules: vec![],
                    recursive: true,
                    include_fragments: vec![],
                }
            )],
            include_fragments: vec![],
            recursive: false,
        }
    )
}

pub Rule: Parsed<Rule> = {
    <r: Rel> => r,
    <u: Uni> => u,
}

pub CommentedRule: Parsed<Rule> = {
    <c: (<Comment>)*> <r: Rule> => r.into_with_docstring(parse_docstrings(c)).0
}

pub Attr: Parsed<Attribute> = {
    <l:@L>
        ":" <s:AttributeIdentifier> "(" <a: (<Symbol>)*> ")"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Attribute {
            name: s,
            arguments: a,
        },
    ),
    <l:@L>
        ":" <s:AttributeIdentifier>
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Attribute {
            name: s,
            arguments: vec![],
        },
    ),
}

pub CommentedAttr: Parsed<Attribute> = {
    <c: (<Comment>)*> <r: Attr> => r.into_with_docstring(parse_docstrings(c)).0
}

pub Grant: Parsed<Grant> = {
    <l:@L>
        <s: Symbol> <sp: ("." <Symbol>)*> ";"
    <r:@R> => {
        let mut vec = vec![s];
        vec.extend(sp);
        Parsed::new_at_loc(
            (l, r),
            Grant::new(vec)
        )
    }
}

pub CommentedGrant: Parsed<Grant> = {
    <c: (<Comment>)*> <g: Grant> => g.into_with_docstring(parse_docstrings(c)).0
}

pub FragmentInclude: Parsed<Identifier> = {
    "#" <f: FragmentIdentifier> ";" => f
}

pub CommentedFragmentInclude: Parsed<Identifier> = {
    <c: (<Comment>)*> <f: FragmentInclude> => f.into_with_docstring(parse_docstrings(c)).0
}


pub Entity: Parsed<Entity> = {
    <l:@L>
        "entity" <e: EntityIdentifier> "{"
            <g: (<CommentedGrant>)*>
            <rs: (<CommentedRelationship>)*>
        "}"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Entity {
            name: e,
            grants: g,
            relationships: rs,
        }
    )
}

pub CommentedEntity: Parsed<Entity> = {
    <c: (<Comment>)*> <e: Entity> => e.into_with_docstring(parse_docstrings(c)).0
}

pub Relationship: Parsed<Relationship> = {
    <l:@L>
        <rel: RuleIdentifier>
            <a: (<CommentedAttr>)*>
            "->" <entity: EntityIdentifier> ";"
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Relationship {
            relationship_name: rel,
            entity_name: entity,
            attributes: a,
        }
    )
}

pub CommentedRelationship: Parsed<Relationship> = {
    <c: (<Comment>)*> <r: Relationship> => r.into_with_docstring(parse_docstrings(c)).0
}

Symbol: String = <s:r"[a-zA-Z][a-zA-Z0-9-_]*|\*"> => s.to_string();

// TODO(zwade): Can i use macros in LALRPOP
EntityIdentifier: Parsed<Identifier> = {
    <l:@L>
        <id: Symbol>
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Identifier::new(IdentifierKind::Entity, id)
    )
}

FragmentIdentifier: Parsed<Identifier> = {
    <l:@L>
        <id: Symbol>
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Identifier::new(IdentifierKind::Fragment, id)
    )
}

RuleIdentifier: Parsed<Identifier> = {
    <l:@L>
        <id: Symbol>
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Identifier::new(IdentifierKind::Rule, id)
    )
}

AttributeIdentifier: Parsed<Identifier> = {
    <l:@L>
        <id: Symbol>
    <r:@R> => Parsed::new_at_loc(
        (l, r),
        Identifier::new(IdentifierKind::Attribute, id)
    )
}

Comment: String = {
    <c: LineComment> => c.to_string(),
    <c: BlockComment> => c.to_string(),
}
